module main
author unknown
version 1 0 
description ''
variables msg2send letter alphabet 'sending-flag' 

  spec ' ' 'blink' 'blink'
  spec ' ' 'letters' 'letters'
  spec ' ' 'flash' 'flash'

to blink {
  setUserLED true
  waitMillis 500
  setUserLED false
  waitMillis 500
}

to flash {
  repeat 3 {
    '[display:mbDisplay]' 33554431
    waitMillis 500
    '[display:mbDisplayOff]'
    waitMillis 500
  }
}

to letters {
  alphabet = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ.!?:()&#+-*/=0123456789'
}

script 855 89 {
to flash {}
}

script 488 95 {
whenStarted
comment 'set to same 
number as the
person(s) you 
want to share
messages with'
'[radio:setGroup]' 0
}

script 226 99 {
whenCondition ('[radio:messageReceived]')
'[display:mbDisplayOff]'
comment 'blink = msg received
tilt face down a little to
display it on LEDs'
repeatUntil (('[sensors:tiltZ]') < -20) {
  blink
}
flash
scroll_text ('[radio:receivedString]')
}

script 689 99 {
to blink {}
}

script 78 104 {
whenStarted
msg2send = ''
letter = 1
letters
'[display:mbDisplay]' 4291652
}

script 472 273 {
whenCondition ('button pressed' 'B')
comment 'Add selected letter to message '
if (not sending-flag) {
  msg2send = ('[data:join]' msg2send (at letter alphabet))
  displayCharacter '+'
  waitMillis 500
  displayCharacter (at letter alphabet)
}
}

script 82 330 {
whenButtonPressed 'A'
comment 'Keep pushing until desired letter shown'
displayCharacter (at letter alphabet)
repeatUntil (not (buttonA)) {
  comment 'tilt left (-x) for backward selection'
  if (('[sensors:tiltX]') < 0) {
    comment 'tilt more to scroll letters faster'
    waitMillis (500 - ((absoluteValue ('[sensors:tiltX]')) * 5))
    letter += -1
  } else {
    comment 'tilt right (+x) for forward selection'
    waitMillis (500 - ((absoluteValue ('[sensors:tiltX]')) * 5))
    letter += 1
  }
  if (letter < 1) {
    letter = (size alphabet)
  } (letter > (size alphabet)) {
    letter = 1
  }
  displayCharacter (at letter alphabet)
}
}

script 477 548 {
whenCondition ('button double pressed' 'B')
comment 'Add a space'
msg2send = ('[data:join]' msg2send ' ')
displayCharacter '_'
waitMillis 500
displayCharacter (at letter alphabet)
}

script 478 761 {
whenCondition ('button long pressed' 'B')
'[display:mbDisplayOff]'
comment 'Preview message'
scroll_text msg2send
waitMillis 500
displayCharacter (at letter alphabet)
}

script 572 966 {
whenCondition (('[sensors:acceleration]') > 280)
comment 'Shake hard to clear 
message to send'
msg2send = ''
'[display:mbDisplay]' 4291652
}

script 82 977 {
to letters {}
}

script 280 1077 {
whenButtonPressed 'A+B'
sending-flag = (booleanConstant true)
'[display:mbDisplayOff]'
comment 'Send message'
'[radio:sendString]' msg2send
scroll_text msg2send
sending-flag = (booleanConstant false)
waitMillis 500
'[display:mbDisplay]' 2269696
}


module 'Basic Sensors' Input
author MicroBlocks
version 1 1 
tags tilt acceleration light sensor 
choices accelerometerRange '1' '2' '4' '8' 
description 'Provides blocks to read tilt in the three axes, acceleration, temperature and light level. Many boards come with this particular set of sensors, such as the micro:bit, the Circuit Playground Express, the Calliope or the Citilab ED1.'

  spec 'r' '[sensors:tiltX]' 'tilt x'
  spec 'r' '[sensors:tiltY]' 'tilt y'
  spec 'r' '[sensors:tiltZ]' 'tilt z'
  spec 'r' '[sensors:acceleration]' 'acceleration'
  spec 'r' '[display:lightLevel]' 'light level'
  spec 'r' '[sensors:temperature]' 'temperature (Â°C)'
  spec ' ' '_setAccelRange' 'set acceleration range _ g = 100' 'menu.accelerometerRange' '1'

to '_setAccelRange' n {
  '[sensors:setAccelerometerRange]' (0 + n)
}


module 'Button Events' Input
author MicroBlocks
version 1 0 
tags event button double long press 
description 'Provides blocks to detect press, long press and double press events on buttons.'
variables _buttons 

  spec 'r' 'button double pressed' 'button _ double pressed' 'auto' 'A'
  spec 'r' 'button long pressed' 'button _ long pressed' 'auto' 5
  spec 'r' 'button pressed' 'button _ pressed' 'auto' 5
  spec 'r' '_button' '_button _' 'auto' 10
  spec ' ' '_updateButtons' '_updateButtons'
  spec 'r' '_read button' '_read button _' 'auto' 10

to '_button' 'pin or name' {
  '_updateButtons'
  for 'each button' _buttons {
    if ((at 1 (v 'each button')) == (v 'pin or name')) {return (v 'each button')}
  }
  comment 'This button has not been initialized. Let''s add it.'
  local 'new button' ('[data:makeList]' (v 'pin or name') '' '' 'waiting')
  '[data:addLast]' (v 'new button') _buttons
  return (v 'new button')
}

to '_read button' 'pin or name' {
  if ((v 'pin or name') == 'A') {
    return (buttonA)
  } ((v 'pin or name') == 'B') {
    return (buttonB)
  } else {
    return (digitalReadOp (v 'pin or name'))
  }
}

to '_updateButtons' {
  if (_buttons == 0) {_buttons = ('[data:makeList]')}
  for button _buttons {
    local 'currently pressed' ('_read button' (at 1 button))
    local 'previous state' (at 4 button)
    if (and ((v 'previous state') == 'waiting') (v 'currently pressed')) {
      comment 'save state change time'
      atPut 2 button (millisOp)
      atPut 3 button (v 'currently pressed')
      atPut 4 button 'pressed'
    } (and ((v 'previous state') == 'pressed') (not (v 'currently pressed'))) {
      local 'press time' ((millisOp) - (at 2 button))
      comment 'There''s been a press. Let''s check how long it was.'
      if ((v 'press time') > 500) {
        atPut 4 button 'long press'
      } ((v 'press time') > 250) {
        atPut 4 button 'single press'
      } else {
        comment 'This is an internal intermediate state.
It could turn out to be a single press or a double press.'
        atPut 2 button (millisOp)
        atPut 4 button 'short press'
      }
    } (and ((v 'previous state') == 'short press') (v 'currently pressed')) {
      atPut 4 button 'double press'
    } (and (and ((v 'previous state') == 'short press') (not (v 'currently pressed'))) (((millisOp) - (at 2 button)) > 250)) {
      atPut 4 button 'single press'
    } (and ((v 'previous state') == 'double press handled') (not (v 'currently pressed'))) {
      comment 'We only go back to "waiting" after a double press if the event has been handled
and the user has released the button. Otherwise we''ll cause false "single press"
positives.'
      atPut 4 button 'waiting'
    }
  }
}

to 'button double pressed' 'pin or name' {
  local 'button' ('_button' (v 'pin or name'))
  local 'state' (at 4 button)
  if (state == 'double press') {
    comment 'We only go back to "waiting" after a double press if the event has been handled
and the user has released the button. Otherwise we''ll cause false "single press"
positives.'
    atPut 4 button 'double press handled'
    return (booleanConstant true)
  }
  return (booleanConstant false)
}

to 'button long pressed' 'pin or name' {
  local 'button' ('_button' (v 'pin or name'))
  local 'state' (at 4 button)
  if (state == 'long press') {
    atPut 4 button 'waiting'
    return (booleanConstant true)
  }
  return (booleanConstant false)
}

to 'button pressed' 'pin or name' {
  local 'button' ('_button' (v 'pin or name'))
  local 'state' (at 4 button)
  if (state == 'single press') {
    atPut 4 button 'waiting'
    return (booleanConstant true)
  }
  return (booleanConstant false)
}


module 'LED Display' Output
author MicroBlocks
version 1 8 
choices led_imageMenu heart 'small heart' yes no happy sad confused angry asleep surprised silly fabulous meh 't-shirt' 'roller skate' duck house tortoise butterfly 'stick figure' ghost sword giraffe skull umbrella snake rabbit cow 'quarter note' 'eight note' pitchfork target triangle 'left triangle' 'chess board' diamond 'small diamond' square 'small square' scissors 
description 'Display primitives for the 5x5 LED display on the BBC micro:bit, Calliope mini and M5Atom Matrix. Boards with TFT displays (such as the Citilab ED1 or the M5Stack family) support these primitives with a simulated "fat pixel" display.'
variables _stop_scrolling_text 

  spec ' ' '[display:mbDisplay]' 'display _' 'microbitDisplay' 15237440
  spec ' ' 'led_displayImage' 'display image _ : x _ y _' 'menu.led_imageMenu num num' 'happy' 1 1
  spec ' ' '[display:mbDisplayOff]' 'clear display'
  space
  spec ' ' '[display:mbPlot]' 'plot x _ y _' 'num num' 3 3
  spec ' ' '[display:mbUnplot]' 'unplot x _ y _' 'num num' 3 3
  space
  spec ' ' 'displayCharacter' 'display character _' 'str' 'A'
  spec ' ' 'scroll_text' 'scroll text _ : pausing _ ms' 'str num' 'HELLO ROSA!' 100
  spec ' ' 'stopScrollingText' 'stop scrolling'
  space
  spec ' ' '_set display color' 'set display color _' 'color'
  space
  spec 'r' '_led_image' 'led image _' 'microbitDisplay' 15237440
  spec 'r' '_led_namedImage' '_led_namedImage _' 'menu.led_imageMenu' 'happy'
  spec 'r' '_led_imageData' '_led_imageData'

to '_led_image' twentyFiveBitInt {
  comment 'An LED image is a 25-bit integer'
  return twentyFiveBitInt
}

to '_led_imageData' {
  return 'heart:4685802,small heart:145728,yes:2269696,no:18157905,happy:15237440,sad:18284864,confused:22348096,angry:23036241,asleep:459616,surprised:4526090,silly:25984017,fabulous:15008639,meh:2236443,t-shirt:15154043,roller skate:11534104,duck:489702,house:10976708,tortoise:359872,butterfly:29332475,stick figure:18158564,ghost:23068334,sword:4657284,giraffe:10946627,skull:15171246,umbrella:6460398,snake:469859,rabbit:16104613,cow:4685361,quarter note:7573636,eight note:7590276,pitchfork:4357813,target:4681156,triangle:1026176,left triangle:32805985,chess board:11184810,diamond:4539716,small diamond:141440,square:33080895,small square:469440,scissors:20287859,'
}

to '_led_namedImage' name {
  local 'data' ('_led_imageData')
  local 'i' ('[data:find]' name data)
  if (i == -1) {
    comment 'Name not found'
    return 0
  }
  local 'start' (('[data:find]' ':' data i) + 1)
  local 'end' (('[data:find]' ',' data i) - 1)
  return ('[data:convertType]' ('[data:copyFromTo]' data start end) 'number')
}

to '_set display color' color {
  callCustomCommand '[display:mbSetColor]' ('[data:makeList]' color)
}

to displayCharacter s {
  s = ('[data:join]' '' s)
  if ((size s) == 0) {
    '[display:mbDisplayOff]'
    return 0
  }
  '[display:mbDrawShape]' ('[display:mbShapeForLetter]' (at 1 s))
}

to led_displayImage imageName optionalX optionalY {
  local 'image' imageName
  if (isType image 'string') {
    image = ('_led_namedImage' imageName)
  }
  '[display:mbDrawShape]' image (argOrDefault 2 1) (argOrDefault 3 1)
}

to scroll_text text optionalDelay {
  text = ('[data:join]' '' text)
  local 'delay' 100
  if ((pushArgCount) > 1) {
    delay = optionalDelay
  }
  _stop_scrolling_text = (booleanConstant false)
  if ('Pico:ed' == (boardType)) {
    for position (((size text) * 6) + 18) {
      if _stop_scrolling_text {return 0}
      '[display:mbDisplayOff]'
      '[tft:text]' text (17 - position) 0 (colorSwatch 125 125 125 255) 1 true
      waitMillis (delay / 2)
    }
  } else {
    for position (((size text) * 6) + 6) {
      if _stop_scrolling_text {return 0}
      for i (size text) {
        '[display:mbDrawShape]' ('[display:mbShapeForLetter]' ('[data:unicodeAt]' i text)) (((i * 6) + 2) - position) 1
      }
      waitMillis delay
    }
  }
}

to stopScrollingText {
  _stop_scrolling_text = (booleanConstant true)
  waitMillis 10
  '[display:mbDisplayOff]'
}


module Radio Comm
author MicroBlocks
version 1 2 
tags radio communication messaging network 
description 'Send and receive messages between micro:bit boards via their built-in radio system.'

  spec ' ' '[radio:sendInteger]' 'radio send number _' 'num' 123
  spec ' ' '[radio:sendString]' 'radio send string _' 'str' 'Hello!'
  spec ' ' '[radio:sendPair]' 'radio send pair _ = _' 'str num' 'light' 10
  spec 'r' '[radio:messageReceived]' 'radio message received?'
  spec 'r' '[radio:receivedInteger]' 'radio last number'
  spec 'r' '[radio:receivedString]' 'radio last string'
  spec ' ' '[radio:setGroup]' 'radio set group _ (0-255)' 'num' 0
  spec ' ' '[radio:setPower]' 'radio set power (0-7) _' 'num' 4
  spec 'r' '_signalStrength' '_radio last signal strength'
  spec 'r' '_radioLastSenderID' '_radio last sender ID'
  spec 'r' '_radioMyID' '_radio my ID'
  spec 'r' '_receivedMessageType' '_radio last message type'
  spec ' ' '_setChannel' '_radio set channel (0-83) _' 'num' 7

to '_radioLastSenderID' {
  return ('[radio:lastMessageID]')
}

to '_radioMyID' {
  return ('[radio:deviceID]')
}

to '_receivedMessageType' {
  return ('[radio:receivedMessageType]')
}

to '_setChannel' channel {
  '[radio:setChannel]' channel
}

to '_signalStrength' {
  return ('[radio:signalStrength]')
}

